apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'
apply from: "$project.rootDir/tools/gradle/script/git-version.gradle"
apply from: "$project.rootDir/tools/gradle/script/quality.gradle"
apply from: "$project.rootDir/tools/gradle/script/java-code-coverage.gradle"

android {
    compileSdkVersion deps.build.compileSdkVersion
    buildToolsVersion deps.build.buildToolsVersion

    defaultConfig {
        applicationId "com.rxmuhammadyoussef.anabeesh"
        minSdkVersion deps.build.minSdkVersion
        targetSdkVersion deps.build.targetSdkVersion
        versionCode deps.build.versionCode
        versionName deps.build.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/tools/proguard/rules-proguard.pro"
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/tools/proguard/rules-proguard-debug.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(":core")

    //UI
    implementation deps.support.design
    implementation deps.support.recyclerview
    implementation deps.support.constraintLayout
    implementation deps.view.circleimageview
    implementation deps.view.roundedImageView
    implementation(deps.view.materialdrawer) {
        transitive = true
    }

    // Arch Component
    implementation deps.architecturecomponents.lifecycles

    // Rx
    implementation deps.rx.java
    implementation deps.rx.relay
    implementation deps.rx.android
    implementation deps.rx.binding
    implementation deps.rx.retrofitAdapter

    // Dependency Injection
    implementation deps.dependencyInjection.dagger
    implementation deps.dependencyInjection.daggerSupportAndroid
    implementation 'com.android.support:support-v4:27.1.0'
    kapt deps.dependencyInjection.daggerCompiler
    kapt deps.dependencyInjection.daggerAndroidCompiler

    // View Binding
    implementation deps.viewBinding.butterknife
    kapt deps.viewBinding.butterknifeCompiler

    // Images
    implementation deps.image.glide
    kapt deps.image.glideCompiler

    // Network
    implementation deps.network.retrofit
    implementation deps.network.retrofitGsonConverter

    // Log
    implementation deps.log.timber
    implementation deps.log.ANRWatchDog
    debugImplementation deps.log.leakCanaryDebug
    releaseImplementation deps.log.leakCanaryRelease

    // Testing
    testImplementation deps.test.junit
    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.espresso

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    configurations.all {
        resolutionStrategy {
            force deps.support.annotations
        }
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex')) {
                details.useVersion "27.0.2"
            }
        }
    }
}
